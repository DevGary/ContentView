plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    namespace 'com.devgary.contentviewdemo'
    compileSdk versions.compileSdk

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        try {
            def propertiesFile = rootProject.file('local.properties')
            def properties = new Properties()
            properties.load(new FileInputStream(propertiesFile))

            buildConfigField "String", "GFYCAT_CLIENT_ID", properties['GFYCAT_CLIENT_ID']
            buildConfigField "String", "GFYCAT_CLIENT_SECRET", properties['GFYCAT_CLIENT_SECRET']

            buildConfigField "String", "IMGUR_AUTHORIZATION_HEADER", properties['IMGUR_AUTHORIZATION_HEADER']
            buildConfigField "String", "IMGUR_MASHAPE_KEY", properties['IMGUR_MASHAPE_KEY']
        }
        catch (Exception e) {
            println("Error parsing local.properties for API keys, placeholder API keys will be created. $e.message")

            buildConfigField "String", "GFYCAT_CLIENT_ID", "\"PLACEHOLDER_GFYCAT_CLIENT_ID\""
            buildConfigField "String", "GFYCAT_CLIENT_SECRET", "\"PLACEHOLDER_GFYCAT_CLIENT_SECRET\""

            buildConfigField "String", "IMGUR_AUTHORIZATION_HEADER", "\"PLACEHOLDER_IMGUR_AUTHORIZATION_HEADER\""
            buildConfigField "String", "IMGUR_MASHAPE_KEY", "\"PLACEHOLDER_IMGUR_MASHAPE_KEY\""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(path: ':core:content-core')
    implementation project(path: ':content-view:content-view')
    implementation project(path: ':content-link-api:content-link-api')
    
    // Using implementation as opposed to testImplementation as we need to access SampleContent file.
    // It is not a big deal since this is just the demo module anyways
    implementation project(path: ':core:test-core') 

    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.core:core-ktx:$versions.androidKtx"
    implementation "com.google.android.material:material:$versions.material"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
    implementation "androidx.navigation:navigation-fragment-ktx:$versions.navigation"

    implementation "com.google.dagger:hilt-android:$versions.daggerHilt"
    kapt "com.google.dagger:hilt-android-compiler:$versions.daggerHilt"
    
    testImplementation "junit:junit:$versions.junit4"
    androidTestImplementation "androidx.test.ext:junit:$versions.junitAndroidExt"
    androidTestImplementation project(path: ':core:test-android-core')
}